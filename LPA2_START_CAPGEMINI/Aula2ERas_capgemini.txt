Algoritmo "Gabarito"

Var
   gabarito: vetor [1..10] de caractere
   respostaAluno: vetor [1..10] de caractere
   opcaoMenu: caractere
   acertos: inteiro

procedimento mostrarMenu()
inicio
   escreval("1 - Cadastrar gabarito")
   escreval("2 - Cadastrar prova")
   escreval("3 - Sair do sistema")
   leia(opcaoMenu)
fimProcedimento

procedimento cadastrarGabarito()
var i: inteiro
inicio
   para i de 1 até 10 faca
      escreval("Digite a resposta da questão ", i, ":")
      leia(gabarito[i])
   fimpara
fimProcedimento

procedimento cadastrarProva()
var i: inteiro
inicio
   acertos <- 0
   para i de 1 até 10 faca
      escreval("Digite a resposta da questão ", i, ":")
      leia(respostaAluno[i])
      se respostaAluno[i] = gabarito[i] entao
         acertos <- acertos + 1
      fimse
   fimpara
   escreval("A nota final do aluno é de ", acertos, "/10")
fimProcedimento

Inicio
   repita
      mostrarMenu()
      escolha opcaoMenu
         caso "1"
            cadastrarGabarito()
         caso "2"
            cadastrarProva()
         caso "3"
            escreval("Saindo do sistema...")
         outrocaso
            escreval("Opção inválida.")
      fimescolha
   até opcaoMenu = "3"
Fimalgoritmo

